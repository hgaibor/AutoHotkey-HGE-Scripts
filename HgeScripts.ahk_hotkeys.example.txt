; After editing this file, rename it remove the .example.txt extension and leave it with .ahk_hotkeys as its final extension
; Despite the odd extension name, its basically an AHK script file, in order to allow to have several scripts running 
; The main code will look for a file that has the same base script name and add the "_hotkeys" extension
; Written for AHK V1, not tested on V2 but may work similarly

; KEY special characters definition 
	; # --> Windows key
	; ^ --> Control key
	; ! --> Alt key
	; <!> --> AltGr key
	; + --> Shift key
	; < --> Left key pair (for control, shift and alt keys)
	; > --> Right key pair (for control, shift and alt keys)
	; SPACE --> Space bar key
	; Numpad0 --> Number pad 0 

; Menu definition
Menu, Tray, Add, 
Menu, Tray, Add, Show hotkeys, +#F1
Menu, Tray, Add, Open ini File, open_ini_file
Menu, Tray, Add, Close Network Messages, LbSearchAndClose
; Hot Keys definition 

	+#x::Gosub, LbSearchAndClose

	+#F1::show_hotkeys_list()  ; Hotkeys mapping information (please fill this list manually below to reflect your changes)
		
	; ^!r::Reload  ; Assign Ctrl-Alt-R as a hotkey to reload the script.
	>+#r::Reload  ; Assign Right Shift-Windows-R as a hotkey to reload the script.
	+#c::AddIPtoRoute() ; This function will add the IP to use the VPN route, parameters defined at the [VPNSettings] section
	+#w::ShowOrRunWebSite("WS") ; Web WhatsApp
	+#s::ShowOrRunWebSite("WebSlack") ; Slack as an app
	
	^#s::ShowOrRunProgram("St3") ; SublimeText
	+#a::ShowOrRunProgram("AIMP") 
	+#z::ShowOrRunProgram("ZimWiki")
	+#r::ShowOrRunProgram("WinRDP") ; Windows remote desktop
	+#p::ShowOrRunProgram("Kitty") ; Putty alternative, very good
	+#t::ShowOrRunProgram("TaskCoach")
	>+#w::ShowOpenWebSiteWithInput("WsSend") ; Ask for mobile number, then incorporate it to a web URL
	
	#MButton:: ToggleAlwaysOnTop() ; Set an app to be always on top

; [HGE] Workaround for lack of additional mouse buttons and really crappy and expensive mice with them
	^MButton:: Send {Browser_Back} ; [HGE] Send browser back
	!MButton:: Send {Browser_Forward} ; [HGE] Send browser forward
	^!Insert::WrittenPaste() ; Send clipboard contents as individual keystrokes, useful when you can't paste into browsers or remote consoles

	;Slot implementation of CreateOpenFolder() function
	>+#e::ProcessFolderSlot() ; Legacy implementation of Folder slots
	+#e::ProcessFolderSlot_X() ; NEW, Mega, unlimited folder slots implementation ^_^

#IfWinActive ahk_class PuTTYConfigBox  ; Shortcuts for Putty Config window 
	; Validation to enable  start button quick key press on Putty
	; Putty Window info
	; KiTTY Configuration
	; ahk_class PuTTYConfigBox
	; ahk_exe KITTY_~1.EXE
	
	; Control for start button
	; ClassNN:	Button2
	; Text:	Start
	!s:: ; PuttyStartSession()  ; Start session rather than opening one 
		ControlFocus , Button2
		ControlSend , Button2, {space}
		return
	
	!a:: ; Focus hostname or address button hotkey
		ControlFocus , Edit1
		return
#IfWinActive

#IfWinActive ahk_class #32770  ; Shortcuts for Putty Accept server session cert window 
	; Validation to enable accept button quick key press on Putty
	; Putty Window info
	; PuTTY Security Alert
	; ahk_class #32770
	; ahk_exe kitty_portable.exe

	; Control for Accept button
	; ClassNN:	Button2
	; Text:	Accept
	!a:: ; PuttyAcceptPrompt()  ; Accept session security prompt
		ControlFocus , Button2
		ControlSend , Button2, {space}
		return
#IfWinActive

; Including general shorcuts information, update lines as needed
show_hotkeys_list() {
	MsgBox,64,%A_ScriptName% script hotkeys,
		(LTrim Join
			=== Currently set hotkeys === `n
			%a_space% - Rshift+Win+r --> Reload this Script `n
			%a_space% - shift+Win+w --> ShowOrRunWebSite("WS") `n
			%a_space% - shift+Win+s --> ShowOrRunWebSite("WebSlack") `n
			%a_space% - shift+Win+c --> AddIPtoRoute (Used for VPN environment) `n
			%a_space% - Ctrl+Win+s --> ShowOrRunProgram("St3") `n
			%a_space% - shift+Win+a --> ShowOrRunProgram("AIMP") `n
			%a_space% - shift+Win+z --> ShowOrRunProgram("ZimWiki") `n
			%a_space% - shift+Win+r --> ShowOrRunProgram("WinRDP") `n
			%a_space% - shift+Win+p --> ShowOrRunProgram("Kitty") `n
			%a_space% - Lshift+Win+w --> ShowOpenWebSiteWithInput("WsSend") `n
			%a_space% - shift+Win+t --> ShowOrRunProgram("TaskCoach") `n
			%a_space% - Win+MouseMidBtn:: ToggleAlwaysOnTop() `n
			%a_space% - Ctrl+MouseMidBtn --> Send "Browser_Back" `n
			%a_space% - Alt+MouseMidBtn --> Send "Browser_Forward" `n
			%a_space% - Ctrl+Alt+Insert --> Write clipboard as key presses `n
			%a_space% - Rshift+Win+e --> Show/Open folder slot (1-9+0), press "e" for list of slots `n
			%a_space% - shift+Win+e --> Show/Open folder X slot (unlimited), press "e + ENTER" for list of slots `n
			`n
			=== In App hotkeys === `n
			%a_space% - Alt+s --> Start Kitty loaded session `n
			%a_space% - Alt+a --> Focus Kitty host address bar `n
			%a_space% - Alt+a --> Accept Kitty host session certificate `n

		)
}

return
LbSearchAndClose:
SearchAndClose("Restoring Network Connections","Alt+F4")
return